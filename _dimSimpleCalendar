let
    // Parameters
    Begin = try BeginDate otherwise #date(Date.Year(DateTime.LocalNow())-5,1,1),  // BeginDate parameter or #date(ThisYear-5,month,day)
    LangCode = "en-US",                                                           // hungarian (magyar) hu-HU, german (német) de-DE
    FirstDayOfWeek = Day.Monday,                                                  // Day.Monday (hétfő), Day.Sunday (vasárnap)
    
    // Generate Data from here 
    ToDate = #date(Date.Year(DateTime.LocalNow())+1,12,31),
    Dates = List.Dates(Begin, Number.From(ToDate)-Number.From(Begin)+1, #duration(1,0,0,0)),
    #"Converted to Table" = Table.FromList(Dates, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
    #"Changed Type" = Table.TransformColumnTypes(#"Converted to Table",{{"Column1", type date}}),
    #"Renamed Columns" = Table.RenameColumns(#"Changed Type",{{"Column1", "Date"}}),
    #"Inserted Year" = Table.AddColumn(#"Renamed Columns", "Year", each Date.Year([Date]), Int64.Type),
    #"Inserted Month" = Table.AddColumn(#"Inserted Year", "Month", each Date.Month([Date]), Int64.Type),
    #"Inserted YearMonth" = Table.AddColumn(#"Inserted Month", "YearMonth", each Text.From([Year]) & "-" & Text.PadStart( Text.From([Month]), 2, "0" ), type text),
    #"Inserted Month Name" = Table.AddColumn(#"Inserted YearMonth", "Month Name", each Date.MonthName([Date], LangCode), type text),
    #"Inserted Quarter" = Table.AddColumn(#"Inserted Month Name", "Quarter", each Date.QuarterOfYear([Date]), Int64.Type),
    #"Inserted Week of Year" = Table.AddColumn(#"Inserted Quarter", "Week of Year", each Date.WeekOfYear([Date], FirstDayOfWeek), Int64.Type),
    #"Inserted Day" = Table.AddColumn(#"Inserted Week of Year", "Day", each Date.Day([Date]), Int64.Type),
    #"Inserted Day of Week" = Table.AddColumn(#"Inserted Day", "Day of Week", each Date.DayOfWeek([Date], FirstDayOfWeek), Int64.Type),
    #"Corrected Day of Week" = Table.TransformColumns(#"Inserted Day of Week", {{"Day of Week", each _ + 1, type number}}),
    #"Inserted Day of Year" = Table.AddColumn(#"Corrected Day of Week", "Day of Year", each Date.DayOfYear([Date]), Int64.Type),
    #"Inserted Day Name" = Table.AddColumn(#"Inserted Day of Year", "Day Name", each Date.DayOfWeekName([Date], LangCode), type text)
in
    #"Inserted Day Name"
